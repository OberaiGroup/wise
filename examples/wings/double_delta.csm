# design parameters associated with wing
despmtr   series_w  4409

dimension  wing     3  5  1

#                     x       y      z   chord  angle
despmtr    wing    " 4.00;  0.00;  0.20;  6.00;  0.00;\
                     7.00;  1.50;  0.20;  3.00;  0.00;\
                     9.00;  4.60;  0.10;  1.00; 20.00;"

# build the wing
mark
   udprim    naca      Series    series_w
   rotatez   -wing[1,5]   0   0
   rotatex   90           0   0
   scale     wing[1,4]
   translate wing[1,1]    wing[1,2]   wing[1,3]

   udprim    naca      Series    series_w
   rotatez   -wing[2,5]   0   0
   rotatex   90           0   0
   scale     wing[2,4]
   translate wing[2,1]    +wing[2,2]   wing[2,3]
rule
store inner_delta

mark
   udprim    naca      Series    series_w
   rotatez   -wing[2,5]   0   0
   rotatex   90           0   0
   scale     wing[2,4]
   translate wing[2,1]    +wing[2,2]   wing[2,3]

   udprim    naca      Series    series_w
   rotatez   -wing[3,5]   0   0
   rotatex   90           0   0
   scale     wing[3,4]
   translate wing[3,1]    +wing[3,2]   wing[3,3]
rule
store outer_delta

restore inner_delta
restore outer_delta

union
store right_wing


restore right_wing
hollow # 0.05 @nbody;7
store right_wing_oml


# Add ribs and spars to inner delta
set num_ribs_in    1
set rib_thickness  0.0
set num_spars_in   2
set spar_thickness 0.0

set span           wing[2,2]-wing[1,2]
set cRoot          wing[1,4]
set cTip           wing[2,4]
set drop           wing[2,1]-wing[1,1]
set sweepAngle     atan2d(drop,span)
set wing_thickness 1.0
set o_x            wing[1,1]
set o_y            wing[1,2]
set o_z            wing[1,3]

udparg $$/even_ribs thickness   rib_thickness
udparg $$/even_ribs span        span
udparg $$/even_ribs c_root      cRoot
udparg $$/even_ribs w_thickness wing_thickness
udparg $$/even_ribs o_x         o_x
udparg $$/even_ribs o_y         o_y
udparg $$/even_ribs o_z         o_z
udparg $$/even_ribs num_ribs    num_ribs_in

udprim $$/even_ribs

restore right_wing

udparg $$/even_spars thickness   spar_thickness
udparg $$/even_spars span        span
udparg $$/even_spars c_root      cRoot
udparg $$/even_spars c_tip       cTip
udparg $$/even_spars sweep_ang   sweepAngle
udparg $$/even_spars w_thickness wing_thickness
udparg $$/even_spars o_x         o_x
udparg $$/even_spars o_y         o_y
udparg $$/even_spars o_z         o_z
udparg $$/even_spars num_spars   num_spars_in

udprim $$/even_spars

patbeg i num_ribs_in+2
  ifthen i eq 1 or i eq num_ribs_in+2
  else
    restore ribs       i 0
    store   inner_ribs i 0
  endif
patend

patbeg i num_spars_in+2
  ifthen i eq 1 or i eq num_spars_in+2
  else
    restore spars       i 0
    store   inner_spars i 0
  endif
patend

restore right_wing

# Add ribs and spars to outer delta
set num_ribs_out   num_ribs_in
set rib_thickness  0.0
set num_spars_out  num_spars_in
set spar_thickness 0.0

set span           wing[3,2]-wing[2,2]
set cRoot          wing[2,4]
set cTip           wing[3,4]
set drop           wing[3,1]-wing[2,1]
set sweepAngle     atan2d(drop,span)
set wing_thickness 1.0
set o_x            wing[2,1]
set o_y            wing[2,2]
set o_z            wing[2,3]

udparg $$/even_ribs thickness   rib_thickness
udparg $$/even_ribs span        span
udparg $$/even_ribs c_root      cRoot
udparg $$/even_ribs w_thickness wing_thickness
udparg $$/even_ribs o_x         o_x
udparg $$/even_ribs o_y         o_y
udparg $$/even_ribs o_z         o_z
udparg $$/even_ribs num_ribs    num_ribs_out
udparg $$/even_ribs sweep_ang   sweepAngle

udprim $$/even_ribs

restore right_wing

udparg $$/even_spars thickness   spar_thickness
udparg $$/even_spars span        span
udparg $$/even_spars c_root      cRoot
udparg $$/even_spars c_tip       cTip
udparg $$/even_spars sweep_ang   sweepAngle
udparg $$/even_spars w_thickness wing_thickness
udparg $$/even_spars o_x         o_x
udparg $$/even_spars o_y         o_y
udparg $$/even_spars o_z         o_z
udparg $$/even_spars num_spars   num_spars_out

udprim $$/even_spars

store right_wing
restore inner_delta
restore outer_delta

patbeg i num_ribs_out+2
  ifthen i eq 1 or i eq num_ribs_out+2
  else
    restore ribs       i 0
    store   outer_ribs i 0
  endif
patend

patbeg i num_spars_out+2
  ifthen i eq 1 or i eq num_spars_out+2
  else
    restore spars       i 0
    store   outer_spars i 0
  endif
patend

patbeg i num_ribs_in+2
  ifthen i eq 1 or i eq num_ribs_in+2
  else
    restore inner_ribs i 0
  endif
patend

patbeg i num_spars_in+2
  ifthen i eq 1 or i eq num_spars_in+2
  else
    restore inner_spars i 0
  endif
patend

patbeg i num_ribs_out+2
  ifthen i eq 1 or i eq num_ribs_out+2
  else
    restore outer_ribs i 0
  endif
patend

patbeg i num_spars_out+2
  ifthen i eq 1 or i eq num_spars_out+2
  else
    restore outer_spars i 0
  endif
patend

end
